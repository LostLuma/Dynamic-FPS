import me.modmuss50.mpp.ReleaseType
import net.fabricmc.loom.task.RemapJarTask

plugins {
	id("dynamic_fps.module")
	alias(libs.plugins.shadow)
	alias(libs.plugins.mod.publish.plugin)
}

repositories {
	// ModMenu
	exclusiveContent {
		filter { includeGroup("com.terraformersmc") }
		forRepository { maven { url = "https://maven.terraformersmc.com/releases" } }
	}
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	accessWidenerPath = project(":platforms:common").loom.accessWidenerPath
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

dependencies {
	modImplementation(libs.fabric.loader)

	modImplementation(fabricApi.module("fabric-resource-loader-v0", libs.versions.fabric.api.get()))
	modImplementation(fabricApi.module("fabric-lifecycle-events-v1", libs.versions.fabric.api.get()))

	modApi(libs.modmenu)

	include(libs.battery)

	common(project(path: ":platforms:common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":platforms:common", configuration: "transformProductionFabric")) { transitive false }
}

shadowJar {
	exclude "architectury.common.json"
	configurations = [project.configurations.shadowCommon]
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar
}

sourcesJar {
	def commonSources = project(":platforms:common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

var modVersion = project.property("mod_version").toString()

publishMods {
	version = modVersion
	displayName = "v${modVersion}"

	type = modVersion.contains("-") ? ReleaseType.BETA : ReleaseType.STABLE;
	modLoaders.addAll(project.property("supported_platforms").toString().split(","))

	changelog = file(rootDir.toPath().resolve("changelog.txt")).text
	file = tasks.withType(RemapJarTask).named("remapJar").get().archiveFile

	curseforge {
		accessToken = providers.environmentVariable("CURSEFORGE_SECRET")
		projectId = "335493"

		/*
		minecraftVersionRange {
			start = project.property("minecraft_version_min").toString()
			end = project.property("minecraft_version_max").toString()
		}
		*/
		minecraftVersions.add(project.property("minecraft_version_min").toString())

		clientRequired = true
		serverRequired = false

		requires("fabric-api")
		optional("cloth-config", "modmenu")
	}

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_SECRET")
		projectId = "LQ3K71Q1"

		/*
		minecraftVersionRange {
			start = project.property("minecraft_version_min").toString()
			end = project.property("minecraft_version_max").toString()

			includeSnapshots = true
		}
		*/
		minecraftVersions.add(project.property("minecraft_version_min").toString())

		requires("fabric-api")
		optional("cloth-config", "modmenu")
	}
}
